plugins {
    id 'application'
    id 'org.owasp.dependencycheck' version '6.0.3'
}

repositories {
    jcenter()
}

dependencies {
    // Template engine
    implementation 'org.apache.velocity:velocity:1.7'
    // Velocity が依存する commons-collections:3.2.1 から明示的にバージョンアップ (CVE-2015-6420, CVE-2017-15708 の対応)
    implementation 'commons-collections:commons-collections:3.2.2'

    // Development tools
    def lombokVersion = '1.18.16'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // Testing
    def junitVersion = '5.6.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation 'com.tngtech.archunit:archunit-junit5:0.14.1'
}

application {
    mainClass = 'com.example.janken.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

run {
    standardInput = System.in
}

jar {
    // 依存関係を含む JAR ファイルをビルドする設定
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }

    manifest {
        attributes 'Main-Class': application.mainClass.orNull
    }
}

dependencyCheck {
    failBuildOnCVSS = 7.0F
}
